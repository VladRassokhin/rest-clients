apply plugin: 'idea'


final kotlinVersion     = '0.1-SNAPSHOT' // http://repository.jetbrains.com/kotlin/org/jetbrains/kotlin/kotlin-runtime/
final guavaVersion      = '13.0-rc1'     // http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22com.google.guava%22%20AND%20a%3A%22guava%22
final googleHttpVersion = '1.10.3-beta'  // http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22com.google.http-client%22%20AND%20a%3A%22google-http-client%22
final slf4jVersion      = '1.6.6'        // http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22org.slf4j%22%20AND%20a%3A%22slf4j-api%22
final logbackVersion    = '1.0.6'        // http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22ch.qos.logback%22%20AND%20a%3A%22logback-classic%22
final junitVersion      = '4.10'         // http://search.maven.org/#search%7Cgav%7C1%7Cg%3A%22junit%22%20AND%20a%3A%22junit%22


defaultTasks 'clean', 'build', 'about', 'install'


buildscript {
    repositories {
        mavenRepo url: 'http://repository.jetbrains.com/kotlin/'
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }
    dependencies {
        classpath 'com.github.goldin.plugins:gradle:0.1.4-SNAPSHOT',
                  'com.github.goldin.plugins:kotlin:0.1.4-SNAPSHOT'
    }
}


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

idea.module {
    iml.withXml {
        provider ->
        provider.node.component.content.first().with {
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/gradle' ])
            appendNode( 'excludeFolder', [ url : 'file://$MODULE_DIR$/out'    ])
        }
    }
}


subprojects {

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'kotlin'        // https://github.com/evgeny-goldin/gradle-plugins
    apply plugin: 'maven'
    apply plugin: 'about'         // http://evgeny-goldin.com/wiki/Gradle-about-plugin
    apply plugin: 'duplicates'    // http://evgeny-goldin.com/wiki/Gradle-duplicates-plugin


    sourceCompatibility  = 1.6
    group                = 'com.github.goldin.rest'

    idea.module {
        downloadSources = true
        downloadJavadoc = true
    }

    repositories {
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }

    about        { dumpDependencies = true }
    duplicates   { configurations   = [ 'runtime' ]
                   verbose          = true }

    dependencies {
        compile     "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                    "org.slf4j:slf4j-api:$slf4jVersion",
                    "com.google.guava:guava:$guavaVersion",
                    "com.google.http-client:google-http-client:$googleHttpVersion"
        runtime     "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
        testCompile "junit:junit:$junitVersion"
        testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
    }

    task sourcesJar( type: Jar, dependsOn: classes ) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task kdocJar( type: Jar, dependsOn: kdoc ) {
         classifier = 'javadoc'
         from kdoc.destinationDir
    }

    artifacts {
         archives sourcesJar
    //     archives kdocJar
    }
}


project( ':artifactory' ) {
    dependencies { compile project( ':common' ) }
}

project( ':teamcity' ) {
    dependencies { compile project( ':common' ) }
}

project( ':youtrack' ) {
    dependencies { compile project( ':common' ) }
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0' }
